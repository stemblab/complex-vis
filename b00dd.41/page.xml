<document>

<code id="resources" lang="coffee" file="resources" show="0" exec="0" out="1">
$pz.resources = -> [
# Handled directly in http.js.
]
</code>

<code id="window" lang="html" file="window" exec="0" show="1" out="1">
<div id="title">
Web Lab
</div>
</code>

<code id="extras" lang="coffee" file="extras" exec="0" show="1" out="1" win="1">
$blab.overloadOps = true
$blab.show "Coffee", false
$blab.show "Eval", false
"b00d2"
"b00dg"
</code>

<code id="css" lang="css" file="main" exec="0" show="1" out="1" win="1">
.center_column {
    width: 960px;
    margin: 0 auto;
}

.left_col {
    float: left;
    width: 400px;
}
.right_col {
    float: right;
    width: 560px;
    margin-top: 40px;
}

.clear {
    clear: both;
}

#main_function {
    margin-top: 10px;
    margin-left: 30px;
}

.info_text {
    color: #aaa;
}

#gallery {
    margin-top: 30px;
}

#examples {
    margin-top: 0px;
}

.example {
    display: inline-block;
    width: 220px;
    margin-bottom: 30px;
    vertical-align: bottom;
}

.mathjax_container {
    margin-bottom: -15px;
    width: 200px;  /* ZZZ larger for main? */
    height: 45px;
    font-size: 10pt;
    overflow: visible;
}

.canvas {
    vertical-align: middle;
}

.slider_thumb {
    position: relative;
    display: inline-block;
    width: 122px;    
}

.slider_text {
    margin-left: 10px;
}

/*.ui-slider .ui-slider-handle {
    height: 12px;
    width: 12px;
    padding-left: 1px;
}*/

/*.ui-slider-horizontal {
    height: .6em;
}*/
</code>

<code id="html" lang="html" file="main" exec="0" show="1" out="1" win="1">
<div class="center_column">
==Interactive Visualization of Complex Functions==

<div class="left_col">
<div id="main_function">
<div id="mathjax" class="mathjax_container"></div>
<a id="canvas_link" target="_blank" title="View or edit code that generated this complex function.">
<canvas id="canvas" width="300", height="300"></canvas>
</a>
<div class="slider" id="slider"></div>
<span id="slider_text" class="slider_text"></span>    
</div>
</div>

<div class="right_col">
* Horizontal and vertical axes: $\Re[z]$ and $\Im[z]$.
* Color $\equiv \arg f(z)$ (<span 
style="color:red">r</span>-<span 
style="color:orange">o</span>-<span 
style="color:#dd0">y</span>-<span 
style="color:green">g</span>-<span 
style="color:blue">b</span>-<span 
style="color:violet">v</span> $\equiv 0\rightarrow2\pi$).
* Lightness $\equiv |f(z)|$ (black$\rightarrow$white $\equiv$ $0\rightarrow\infty$).
* *Click image* to view/edit code that generated it.<br>
<br>
__These are not animated GIFs.  The computation and visualization are performed on-the-fly.__
</div>

<div class="clear"></div>

<div id="gallery">
==Gallery==
<span class="info_text">Click to see larger image</span>
<div id="examples"></div>
[/m/b00dh, Add a function to this gallery]
</div>
    
</div>
</code>

<code id="coffee" lang="coffee" file="main" exec="0" show="1" out="1" win="1">
#!vanilla
class MainComplexFunction

    selectGalleryExample: "selectGalleryExample"

    constructor: ->
        @doc = $ document
        @doc.trigger "reprocessHtml"
        @canvasId = "canvas"
        @doc.unbind @selectGalleryExample
        @first = true
        @doc.on @selectGalleryExample, (e, data) =>
            @func?.text "Loading..."
            # Delay so loading message displayed.
            setTimeout (=> @setExample data.functionSpec, data.id), 10
    
    setExample: (functionSpec, id) ->
        @draw functionSpec, id
        if @first
            @animate functionSpec
            @first = false
        else
            @changed = true        
    
    draw: (functionSpec, id) ->
        $("#canvas_link").attr href: "/m/#{id}"
        spec = $.extend {}, functionSpec  # Clone
        spec.canvasId = @canvasId
        spec.mathJaxContainer = $ "#mathjax"
        spec.sliderContainer = $ "#slider"
        spec.sliderText = $ "#slider_text"
        @func = new ComplexFunction spec
        @doc.trigger "htmlOutputUpdated"  # To re-render MathJax
    
    animate: (spec) ->
        a = spec.a
        setSlider = (v) =>
            return if @func.sliderTouched? or @changed?
            @func.setSliderVal v
            setTimeout (-> setSlider(v+a.step)), 1000 if v<a.max
        setSlider a.min

class Gallery

    # Blab id of examples to show in gallery
    examplesId: "b00dh"

    # Exclude these revisions from gallery
    excludeRevisions: [1]
    
    constructor: ->
        @div = $ "#examples"
        @examples = []
        @getRevisions()
                
    getRevisions: ->
        spec = 
            id: @examplesId
            action: "getLabRevisions"
        $blab.server.request spec, (resp) =>
            return unless resp.ok
            @div.empty()
            @examplesToLoad = 0
            @getRevision r.rev for r in resp.revisions
                
    getRevision: (rev) ->
        return if parseInt(rev) in @excludeRevisions
        id = "#{@examplesId}.#{rev}"
        @examplesToLoad++
        @examples.push(new GalleryExample @div, id, => @exampleLoaded())
                
    exampleLoaded: ->
        @examplesToLoad--
        @examples[0].select() if @examplesToLoad is 0  # All examples loaded

class GalleryExample

    constructor: (@container, @id, @loadedCallback) ->
        @html()
        @load()
        $(document).on "clickGalleryExample", (e, data) => @border(data.id is @id) 
          
    html: ->
        
        @div = $ "<div>", class: "example"
        @container.append @div
        
        @mathJaxContainer = $ "<div>", class: "mathjax_container"

        @canvasId = "canvas_"+@id
        @link = $ "<a>", click: => @select()
        @sliderContainer = $ "<div>", class: "slider_thumb"
        @sliderText = $ "<span>", class: "slider_text"
        
        @div
            .append(@mathJaxContainer)
            .append(@link)
            .append(@sliderContainer)
            .append(@sliderText)
        
        @canvas = $ "<canvas>",
            id: @canvasId
            class: "canvas"
            width: 120
            height: 120
            title: "Click to see larger image (at top)"
            
        @link.append @canvas
        @border false
        
    load: ->
        t = new Date().getTime()
        url = "/puzlet/php/source.php?pageId=#{@id}&file=main.coffee&t=#{t}"
            
        $.ajax(
            url: url
            type: "get"
        ).done (data) =>
            @runCoffee data
            @loadedCallback()
            
    runCoffee: (coffee) ->
        
        @js = CoffeeEvaluator.compile coffee
                
        # Global function used in imported/compiled CoffeeScript code.
        $blab.complexFunction = (@functionSpec) =>
            spec = $.extend({}, @functionSpec)  # Clone
            spec.canvasId = @canvasId
            spec.mathJaxContainer = @mathJaxContainer
            spec.link = @link
            spec.sliderContainer = @sliderContainer
            spec.sliderText = @sliderText
            new ComplexFunction spec
            
        eval @js
        
    select: ->
        @border()
        data =
            id: @id
            functionSpec: @functionSpec
        $.event.trigger "clickGalleryExample", data
        # Delay computation so borders can be set/unset.
        setTimeout (-> $.event.trigger "selectGalleryExample", data), 10
                
    border: (border=true) ->
        @canvas.css border: (if border then "2px solid blue" else "2px solid white") 
        
class ComplexFunction

    constructor: (@spec) ->
        
        # MathJax label
        mathJax = @spec.mathJax ? "?="       
        @spec.mathJaxContainer.html("$f(z) = #{mathJax}$")

        # Slider if parameter property specified
        if @spec.a?
            a = @spec.a
            @spec.sliderContainer.slider
                min: a.min
                max: a.max
                step: a.step
                value: a.init
                slide: (e, ui) =>
                    @sliderTouched = true
                    @setSliderText ui.value           
                change: (e, ui) => @draw ui.value
        
        # Draw complex function.
        @draw @spec.a?.init
            
    draw: (a) ->
        if a?
            @setSliderText a
            f = (z) => @spec.f z, a
        else
            f = @spec.f
        xMax = @spec.xMax
        canvasId = @spec.canvasId
        new $blab.ComplexFunctionImage {f, xMax, canvasId}, $blab.colorMap
        
    text: (txt) ->
        canvas = document.getElementById @spec.canvasId
        context = canvas.getContext "2d"
        context.fillStyle = "white"
        context.font = "bold 16px Arial"
        context.textAlign = "center"
        w = canvas.width
        h = canvas.height
        context.fillText txt, w/2, h/2
        
    setSliderVal: (a) ->
        @spec.sliderContainer.slider "value", a
        
    setSliderText: (val) => @spec.sliderText.html "a=#{val}"

new MainComplexFunction
new Gallery
</code>

<code id="result" lang="coffee" file="result" exec="0" show="1" out="1">

</code>

<code id="js" lang="javascript" file="main" exec="0" show="1" out="1">

</code>

</document>

